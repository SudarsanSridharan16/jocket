plugins {
  id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.github.pcdv.jocket'
version = "${jocket_version}"

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

task jni(type: Exec) {
  workingDir 'src/main/c'
  executable 'make'
}
jni.dependsOn compileJava

import org.gradle.internal.os.OperatingSystem

if (OperatingSystem.current().isLinux()) {
  test.dependsOn jni
  jar.dependsOn jni
} else
  println("Disabling JNI build because OS is not UNIX")

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['MyPublication']
  publish = true

  pkg {
    repo = 'maven'
    name = 'jocket'
    userOrg = System.getenv('BINTRAY_USER')
    licenses = ['MIT']
    vcsUrl = 'https://github.com/pcdv/jocket.git'
    version {
      name = "${jocket_version}"
      desc = "Experimental shared memory socket ${jocket_version}"
      released = new Date()
    }
  }
}


publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'com.github.pcdv.jocket'
      artifactId 'jocket'
      version "${version}"

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}
